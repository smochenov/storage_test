
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокРазделов();
	
	ОписаниеПоследнегоОбновления = "Обновлено 5 минут назад";

	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРазделов()

	Разделы.Очистить();

	Объекты = ПолучитьИнформациюОГорящихОбъектах();
	
	СтрокаРаздела = Разделы.Добавить();
	СтрокаРаздела.Картинка   = БиблиотекаКартинок.ИконкаЗаявкаНаРасходДС;
	СтрокаРаздела.ИмяРаздела = "ЗаявкиНаРасходДС";
	СтрокаРаздела.Описание   = "Заявки на расход ДС";
	ЗаполнитьОписаниеОВажныхСобытиях(Объекты, СтрокаРаздела);
	
	СтрокаРаздела = Разделы.Добавить();
	СтрокаРаздела.Картинка   = БиблиотекаКартинок.ИконкаСогласование;
	СтрокаРаздела.ИмяРаздела = "ЗаявкиНаСогласование";
	СтрокаРаздела.Описание   = "Заявки на согласование";
	ЗаполнитьОписаниеОВажныхСобытиях(Объекты, СтрокаРаздела);

	СтрокаРаздела = Разделы.Добавить();
	СтрокаРаздела.Картинка   = БиблиотекаКартинок.ИконкаРеестр;
	СтрокаРаздела.ИмяРаздела = "РеестрыПлатежей";
	СтрокаРаздела.Описание   = "Реестры платежей";
	ЗаполнитьОписаниеОВажныхСобытиях(Объекты, СтрокаРаздела);
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Процедура ЗаполнитьОписаниеОВажныхСобытиях(Объекты, СтрокаРаздела)

	ТекстВРаботе     = "В работе";
	ТекстНеПрочитано = "Не прочитано";
	ТекстПросрочено  = "Просрочено";  	
	
	СтрочкаДанных = Объекты.Найти(СтрокаРаздела.ИмяРаздела);
	НеНашли = (СтрочкаДанных = Неопределено); 
	
	ВРаботе     = СтрШаблон(ТекстВРаботе     + ": %1", ?(НеНашли, 0 ,СтрочкаДанных.ВРаботе));
	НеПрочитано = СтрШаблон(ТекстНеПрочитано + ": %1", ?(НеНашли, 0 ,СтрочкаДанных.НеПрочитано));
	Просрочено  = СтрШаблон(ТекстПросрочено  + ": %1", ?(НеНашли, 0 ,СтрочкаДанных.Просрочено));
	
	СтрокаРаздела.ВажныеДанные = ВРаботе + Символы.ПС + НеПрочитано + Символы.ПС + Просрочено; 			

КонецПроцедуры
	
&НаСервереБезКонтекста
Функция ПолучитьИнформациюОГорящихОбъектах()

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТекущаяДата", КонецДня(ТекущаяДата()));
	Запрос.Текст = "ВЫБРАТЬ
	               |	""ЗаявкиНаРасходДС"" КАК Раздел,
	               |	Документ.Ссылка КАК Ссылка,
	               |	1 КАК ВРаботе,
	               |	ВЫБОР
	               |		КОГДА НЕ Документ.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |				И Документ.СрокИсполнения < &ТекущаяДата
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Просрочено,
	               |	ВЫБОР
	               |		КОГДА НЕ Документ.Прочитан
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК НеПрочитано,
	               |	Документ.СрокИсполнения КАК СрокИсполнения
	               |ИЗ
	               |	Документ.ЗаявкаНаРасходованиеСредств КАК Документ
	               |ГДЕ
	               |	НЕ Документ.Выполнена
	               |	И НЕ Документ.ПометкаУдаления
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""ЗаявкиНаСогласование"",
	               |	ЗаявкаНаСогласование.Ссылка,
	               |	1,
	               |	ВЫБОР
	               |		КОГДА НЕ ЗаявкаНаСогласование.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |				И ЗаявкаНаСогласование.СрокИсполнения < &ТекущаяДата
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА НЕ ЗаявкаНаСогласование.Прочитан
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ЗаявкаНаСогласование.СрокИсполнения
	               |ИЗ
	               |	Документ.ЗаявкаНаСогласование КАК ЗаявкаНаСогласование
	               |ГДЕ
	               |	НЕ ЗаявкаНаСогласование.Выполнена
	               |	И НЕ ЗаявкаНаСогласование.ПометкаУдаления
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	""РеестрыПлатежей"",
	               |	РеестрПлатежей.Ссылка,
	               |	1,
	               |	ВЫБОР
	               |		КОГДА НЕ РеестрПлатежей.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |				И РеестрПлатежей.СрокИсполнения < &ТекущаяДата
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА НЕ РеестрПлатежей.Прочитан
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	РеестрПлатежей.СрокИсполнения
	               |ИЗ
	               |	Документ.РеестрПлатежей КАК РеестрПлатежей
	               |ГДЕ
	               |	НЕ РеестрПлатежей.Выполнена
	               |	И НЕ РеестрПлатежей.ПометкаУдаления";
		
	Данные = Запрос.Выполнить().Выгрузить();
	Данные.Свернуть("Раздел", "ВРаботе, НеПрочитано, Просрочено");

	Возврат Данные;

КонецФункции

&НаКлиенте
Процедура РазделыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Если Элементы.Разделы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Раздел = Элементы.Разделы.ТекущиеДанные.ИмяРаздела;

	ПараметрыВыполненияКоманды = Новый Структура();
	ПараметрыВыполненияКоманды.Вставить("Источник", ЭтаФорма);
	ПараметрыВыполненияКоманды.Вставить("Уникальность",Новый УникальныйИдентификатор);
	ПараметрыВыполненияКоманды.Вставить("Окно", Неопределено);

	Если Раздел = "ЗаявкиНаРасходДС" Тогда

		ПараметрыВыполненияКоманды.Вставить("НавигационнаяСсылка", 
			"e1cib/navigationpoint/desktop/ОбщаяКоманда.ЗаявкиНаРасходДС");
		ОткрытьФорму("ОбщаяФорма.СписокЗаявокНаРасходованиеДС",,
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);  	
		
	ИначеЕсли Раздел = "ЗаявкиНаСогласование" Тогда

		ПараметрыВыполненияКоманды.Вставить("НавигационнаяСсылка", 
			"e1cib/navigationpoint/desktop/ОбщаяКоманда.ЗаявкиНаСогласование");
		ОткрытьФорму("ОбщаяФорма.СписокЗаявокНаСогласование",,
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		
	ИначеЕсли Раздел = "РеестрыПлатежей" Тогда

		ПараметрыВыполненияКоманды.Вставить("НавигационнаяСсылка", 
			"e1cib/navigationpoint/desktop/ОбщаяКоманда.РеестрыПлатежей");
		ОткрытьФорму("ОбщаяФорма.СписокРеестровПлатежей",,
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);	
		
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ЗаполнитьСписокРазделов();
КонецПроцедуры

