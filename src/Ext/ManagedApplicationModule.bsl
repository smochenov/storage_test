
Процедура ПриНачалеРаботыСистемы()
	
	//Проверка Кода доступа
	Если ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("КодДоступа") <> "" Тогда
		Результат = ОткрытьФормуМодально("ОбщаяФорма.ВводКодаДоступаВПриложение");
		Если Результат = Неопределено ИЛИ НЕ Результат Тогда
			ЗавершитьРаботуСистемы(Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
			
	#Если МобильноеПриложениеКлиент Тогда
		
	//Push
	ДоставляемыеУведомления.ПодключитьОбработчикУведомлений("ПриПолученииУведомления");
	ОбновитьIDПодписчика(); 
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьIDПодписчика", 7200);
		
	#КонецЕсли
	
КонецПроцедуры


#Область Push_Уведомления

Процедура Подключаемый_ОбновитьIDПодписчика() Экспорт
	
	ОбновитьIDПодписчика();
	
КонецПроцедуры

Процедура ОбновитьIDПодписчика()

	#Если МобильноеПриложениеКлиент Тогда

		НомерПроекта = PushУведомленияВызовСервера.ПолучитьНомерПриложенияGCM();
		
		Если ПустаяСтрока(НомерПроекта) Тогда
			Возврат		
		КонецЕсли; 
		
		Попытка
			IDПодписчика = ДоставляемыеУведомления.ПолучитьИдентификаторПодписчикаУведомлений(НомерПроекта);
		Исключение
			ПротоколСобытийВызовСервера.ДобавитьОшибку("Получение User Push ID", ОписаниеОшибки()); 			
			Возврат
		КонецПопытки;
					
		PushУведомленияВызовСервера.ОтправитьIDПодписчикаНаСервер(IDПодписчика);
		
	#КонецЕсли

КонецПроцедуры

Процедура ПриПолученииУведомления(Уведомление, Локальное, Показано) Экспорт
	
	ПротоколСобытийВызовСервера.ДобавитьИнформацию("Push-уведомление", Уведомление.Текст);
		
КонецПроцедуры

#КонецОбласти
